areaRadius = 8 #20

# When called returns a list of points that forms a convex hull around 
#   the listPts Given (http://adultstudylife.blogspot.com/2016/06/quick-hull-in-python.html)
function QuickHull(focalpts) {
    var listPts = {}
    var hullPts = {}
    jp = 0
    #save current goal
    save_goal = cur_goal
    foreach(focalpts, function(key, pt) {
        var ip = 0
        var totip = 12
        while(ip<totip){
            new_pt = math.vec2.add(pt,math.vec2.newp(areaRadius, 2*ip*math.pi/totip))
            pt_gf = {.0=new_pt, .1=vec_from_gps(GPSlimit[1].lat, GPSlimit[1].lng, 0), .2=vec_from_gps(GPSlimit[2].lat, GPSlimit[2].lng, 0), .3=vec_from_gps(GPSlimit[3].lat, GPSlimit[3].lng, 0), .4=vec_from_gps(GPSlimit[4].lat, GPSlimit[4].lng, 0)}
		    geofence(pt_gf)
            # takes the update goal from geofence
            if(cur_goal.latitude!=save_goal.latitude) {
                new_pt = vec_from_gps(cur_goal.latitude, cur_goal.longitude, 0)
                storegoal(save_goal.latitude, save_goal.longitude, pose.position.altitude)
            }
            listPts[jp*totip+ip]=new_pt   
            #log("Created new pt: ", listPts[jp*6+ip].x, listPts[jp*6+ip].y, "(", pt.x, pt.y, ")")
            ip=ip+1
        }
        jp=jp+1
    })

    #log(size(listPts),jp)

    # get the min, and max from the list of points
    min_max = get_min_max_x(listPts)

    #tableofvec_print(min_max)

    hullPts = quickhull_rec(hullPts, listPts, min_max.min, min_max.max)

    hullPts = quickhull_rec(hullPts, listPts, min_max.max, min_max.min)

    return hullPts
} 

# Does the sorting for the quick hull sorting algorithm
function quickhull_rec(hullPts, listPts, minp, maxp) {
    var left_of_line_pts = get_points_left_of_line(minp, maxp, listPts)

    var ptC = point_max_from_line(minp, maxp, left_of_line_pts)

    if(size(ptC) < 1){
        hullPts[size(hullPts)] = maxp    # max
        return hullPts
        #log("Add pt:", maxp.x, maxp.y)
    }

    hullPts = quickhull_rec(hullPts, left_of_line_pts, minp, ptC)

    hullPts = quickhull_rec(hullPts, left_of_line_pts, ptC, maxp)

    return hullPts
}

# Returns all points that are LEFT of a line start->end
function get_points_left_of_line(minp, maxp, listPts) {
    left_pts = {}

    foreach(listPts, function(key, pt) {
        if(isCCW(minp, maxp, pt)){
            left_pts[size(left_pts)]=pt
        }   
    })

    return left_pts
}

# Returns the maximum point from a line start->end
function point_max_from_line(minp, maxp, points) {
    max_dist = 0
    max_point = {}

    foreach(points, function(key, point) {
        if((not(math.vec2.equal(point, minp))) and (not(math.vec2.equal(point, maxp)))) {
            #log("Get distance of pt: ", point.x, point.y)
            dist = distance_toline(minp, maxp, point)
            if(dist > max_dist) {
                max_dist = dist
                max_point = point
            }
        }
    })

    return max_point
}

function get_min_max_x(list_pts) {
    min_x = 100000.0
    max_x = -100000.0
    min_y = 0.0
    max_y = 0.0

    foreach(list_pts, function(key, point) {
        if(point.x < min_x){
            #log("In min: ", point.x, point.y)
            min_x = point.x
            min_y = point.y
        }
        if(point.x > max_x){
            #log("In max: ", point.x, point.y)
            max_x = point.x
            max_y = point.y
        }
    })

    return {.min=math.vec2.new(min_x, min_y), .max=math.vec2.new(max_x, max_y)}
}

# Given a line of start->end, will return the distance that
#    point, pt, is from the line.
function distance_toline(start, end, pt) { # pt is the point
    var x1 = start.x
    var y1 = start.y
    var x2 = end.x
    var y2 = end.y
    var x0 = pt.x
    var y0 = pt.y
    var nom = math.abs((y2 - y1) * x0 - (x2 - x1) * y0 + x2 * y1 - y2 * x1)
    var denom = math.vec2.length(math.vec2.sub(end,start))
    var result = nom / denom
    return result
}

# Tests whether the turn formed by A, B, and C is ccw
function isCCW(A, B, C){
    return (B.x - A.x) * (C.y - A.y) > (B.y - A.y) * (C.x - A.x)
}